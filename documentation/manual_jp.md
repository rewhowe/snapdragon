[English](./manual.md)

## 前置き

下手な日本語ですみません。

## 目次

* [変数について](#変数について)
  * [数値](#数値)
  * [文字列](#文字列)
  * [配列](#配列)
  * [配列・文字列の属性](#配列文字列の属性)
  * [ブーリアン型](#ブーリアン型)
  * [ヌル](#ヌル)
  * [それ・あれ](#それあれ)
* [制限公文について](#制限公文について)
  * [条件分岐](#条件分岐)
  * [複数条件分岐](#複数条件分岐)
  * [繰り返し（反復）](#繰り返し反復)
  * [例外処理](#例外処理)
* [関数について](#関数について)
  * [関数の定義](#関数の定義)
  * [関数の呼び出し](#関数の呼び出し)
  * [活用（動詞の語形変化）](#活用動詞の語形変化)
* [雑題](#雑題)
  * [インデント](#インデント)
  * [改行](#改行)
  * [無演算命令](#無演算命令)
  * [コメント](#コメント)
  * [約物（句読文字）](#約物句読文字)
  * [スクリプト終了](#スクリプト終了)
  * [デバッグ](#デバッグ)
* [ビルトイン関数（組み込み関数）](#ビルトイン関数組み込み関数)
  * [出力](#出力)
  * [フォーマット](#フォーマット)
  * [文字列・配列の処理](#文字列配列の処理)
  * [算数](#算数)
  * [雑用](#雑用)

## 変数について

変数の定義は次のようなフォーマット：`【変数名】は 【バリュー】`。

例）

```
ホゲは 1
```

`ホゲ`という`1`のバリューを持つ変数が作られます。

予約語に一致したり、無効文字を含むもの以外、変数名は殆ど無制限です。無効文字：`\`（バックスラッシュ、詳しくは「[改行](#改行)」より参照してください）、`￥ｎ`（二文字、円記号＋全角「ｎ」）、`【`、`】`。予約語の属性に一致する変数名より、属性が優先されます。詳しくは[配列・文字列の属性](#配列文字列の属性)より参照してください。


変数は初期値と共に定義しなくてはなりません。単純なバリューを直接に引数として関数に渡すことが出来ます。

### 数値

数値は次のようなフォーマット：`/-?(\d+\.\d+|\d+)/`（即ちマイナスや浮動小数点数可能）。全角数字も可能です。

例）

```
ホゲは -3.14
```

### 文字列

文字列は`「`で始まって、`」`で終わります。

例）

```
ホゲは 「もじれつ」
```

`」`の前に`\`を付けると文字通りの`」`を書くことが出来ます。

例）

```
ホゲは 「文字列の中の「もじれつ\」」
```

複数行の文字列も可能です。先頭のスペース、末尾のスペース、あと改行も含まれて省略されます。`\n`または`￥ｎ`を書き込むと改行を挿入出来ます。更に`\`を前に付けると改行しません。

```
作文は 「こんにちは。
         今日の予定は特になし。
         週末にカツ丼を食べに行く。」

「カツ丼が好き。￥ｎ
　毎日食べても飽きない。」を 言う
```

#### 文字列補間

文字列の中に変数や属性をすみつきカッコ（`【`と`】`）で囲で書き込むと、その値を補間することが出来ます。バックスラッシュ（`\`）を前に入力すると、単純なすみつきパレーンとなります。

```
名前は 「世界」
「こんにちは【名前】！」を 言う
```

文字列に補間する値はプリミティブ値（単純な整数や文字列など）であることや、更に補間する文字列を含むことが不可能です。

例）

```
「世界【1】の美女は誰？」と 言う      ※ NG
「壁の【「鏡」】に聞いてみた」と 言う ※ NG

僕は 連想配列
僕の 「名前」は 「リュウ」

「こんにちは【僕の 「名前」】！」と 言う       ※ OK
キー名は 「名前」
「こんにちは【僕の キー名】！」と 言う         ※ OK
「こんにちは【僕の 「【キー名】」】！」と 言う ※ NG
```

ブーリアン型の真が「はい」となり、偽が「いいえ」となります。ヌルは空となります。

文字列は、実際には文字の配列として考えることもありです。文字列の中の文字の扱いについては、[連想配列](#連想配列ハッシュ)より参照してください。

### 配列

配列は一つ以上のバリューを持つカンマ（全角・半角可能）区切りのリストです。

例）

```
ホゲは 1、2、3
フガは 1,2,3
ピヨは 「あ」、「い」、「う」、1、2、3、ホゲ
```

`配列`というキーワードで空の配列の定義が出来ます。`連想配列`も可能です。詳しくは次のセクションより参照してください。

例）

```
ホゲは 配列
```

リストが長すぎる時は、カンマの後に折り返しが出来ます。

```
母音は 「あ」、
　　　 「い」、
　　　 「う」、
　　　 「え」、
　　　 「お」
```

複数行の文字列と同じく、先頭・末尾のスペースが無視されますが、`\`と改行で揃わせることが出来ます。詳しくは「[改行](#改行)」より参照してください。

```
参加者は \
　「ウイ」、
　「チャールス」、
　「ジャック＆
　　アジューラ」
```

#### 連想配列（ハッシュ）

配列（と文字列）は連想配列の使い方も可能です。

配列の要素の取得方法は3つ：

* 指数：`【整数】【助数詞】目`。使用可能の助数詞は：`つ`、`人`、`個`、`件`、`匹`、と`文字`。例：`ホゲの 1つ目`
* キー名：単純な文字列。補間も可能。例：`ホゲの 「キー名」`
* キー変数：単純なな既存の変数。「それ」「あれ」も可能。例：`ホゲの 変数`

指数の場合は1オリジンです。「1つ目」は文字通りに配列の先頭の要素（最低指数、添字の0）です。「0つ目」はありません。しかし、キー名やキーを持つ変数の場合、プログラミングの普段通りに0オリジンです。

存在しないキーまたは指数が要素数より大きい、マイナスの場合は、ヌルとなります。

取得方法によると、キーは数字でも文字でも可能です。実は、内部的にはキーが全て「浮動小数点化文字列」の扱いとなっています。次のような取得は同じ：

```
ホゲの 1つ目

ホゲの 「0」

ホゲの 「0.0」

整数の指数は 0
ホゲの 整数の指数

浮動小数点の指数は 0.0
ホゲの 浮動小数点の指数
```

文字列の場合、正常なキーは数字かつ0から文字数までの範囲に限られています。

```
「ホゲ」の １つ目 ※ OK

「ホゲ」の 「0」  ※ OK

文字の位置は 0
「ホゲ」の 文字の位置 ※ OK

「ホゲ」の 「キー名」 ※ NG
```

普段、全角数字は半角数字と区別していません。それと違って、指数の場合は別々として認識しています。次のような取得は同じではありません：

```
ホゲの 「0」

ホゲの 「０」
```

配列の要素の追加に関しては、キーが数値や文字列とは違うデータ型の場合、そのキーが文字列化されます。特に、`真`が`はい`となり、`偽`が`いいえ`となります。ヌルが空の文字列と同じく扱われます。

殆どの場合、新しく追加した要素は追加順となります。キーが既存の最高指数の次の整数となります。

例）

```
例の配列は 連想配列
例の配列の 1つ目は    「あ」 ※ {0: "あ"}
例の配列の 「ほげ」は 「い」 ※ {0: "あ", "ほげ": "い"}
例の配列の 「4.6」は  「う」 ※ {0: "あ", "ほげ": "い", 4.6: "う"}
例の配列の 「ふが」は 「え」 ※ {0: "あ", "ほげ": "い", 4.6: "う", "ふが": "え"}
例の配列に 「お」を 押し込む ※ {0: "あ", "ほげ": "い", 4.6: "う", "ふが": "え", 5: "お"}
```

要素が自由に、何の指数にも紐付くことがある為、`先頭から押し込む`と`先頭を引き出す`という配列の先頭を変更する処理は数値のキーを全てリセットします。`抜く`等の自由に配列の中身を変更する処理はリセットしません。

例）

```
例の配列Ａは 連想配列
例の配列Ａの 1つ目は   「あ」          ※ {0: "あ"}
例の配列Ａの 「4.6」は 「い」          ※ {0: "あ", 4.6: "う"}
例の配列Ａに 「う」を 先頭から押し込む ※ {0: "う", 1: "あ", 2: "う"}

例の配列Ｂは 連想配列
例の配列Ｂの 1つ目は   「あ」 ※ {0: "あ"}
例の配列Ｂの 「4.9」は 「い」 ※ {0: "あ", 4.9: "う"}
例の配列Ｂから 先頭を引き出す ※ {0: "う"}
```

配列の結合の場合：

* 対象列の最高指数に続いて、要素列の指数がリセットされます
* 要素列にあるキー名が、対象列にも存在する場合、対象列の要素が上書きされます
* 上のシナリオに当てはまらない対象列のキーはそのまま変わりません

```
例の配列Ａは 配列
例の配列Ａの 1つ目は    「あ」
例の配列Ａの 「ほげ」は 「い」
例の配列Ａの 「4.6」は  「う」
例の配列Ａの 「ふが」は 「え」
※ {0: "あ", "ほげ": "い", 4.6: "う", "ふが": "え"}

例の配列Ｂは 配列
例の配列Ｂの 1つ目は    「か」
例の配列Ｂの 「ほげ」は 「き」
例の配列Ｂの 「4.9」は  「く」
例の配列Ｂの 「ぴよ」は 「け」
※ {0: "か", "ほげ": "き", 4.9: "く", "ぴよ": "け"}

例の配列Ａに 例の配列Ｂを 結合する
※ {0: "あ", "ほげ": "き", 4.6: "う", "ふが": "え", 5: "か", 6: "く", "ぴよ": "け"}
```

もしかして若干複雑に見えるかもしれませんが、実際には数値のキーと文字列のキーを交えることが少ないと思われます。

### 配列・文字列の属性

配列または文字列の属性を取得するには、次のようなフォーマット：`【変数】の 【属性】`。変数だけではなくて、単純な文字列にも属性を取得出来ます。

下記は使用可能の属性：

| 属性     | 対象           | 代入可能？ | 詳細 |
| -------- | -------------- | ---------- | ---- |
| 長さ     | 配列 or 文字列 | 不可       | 配列の要素数または文字列の文字数 |
| キー列   | 配列 only      | 不可       | 配列がキーの配列 |
| 先頭     | 配列 or 文字列 | 可         | 配列の先頭の要素、または文字列の最初の文字<br>空の場合は空の配列、または空の文字列 |
| 末尾     | 配列 or 文字列 | 可         | 配列の末尾の要素、または文字列の最後の文字<br>空の場合は空の配列、または空の文字列 |
| 先頭以外 | 配列 or 文字列 | 不可       | 先頭の要素以外の要素を持つ配列、または先頭以外の文字列<br>空の場合は空の配列、または空の文字列 |
| 末尾以外 | 配列 or 文字列 | 不可       | 末尾の要素以外の要素を持つ配列、または末尾以外の文字列<br>空の場合は空の配列、または空の文字列 |

`長さ`のエイリアスは`ながさ`、`大きさ`、`おおきさ`、`数`、`かず`、`人数`、`個数`、`件数`、`匹数`、`文字数`。

```
チームは 「アジューラ」、「チャールス」、「ウイ」
チームの 「サポート」は 「ニッキー」
チームの 「リーダー」は 「セフ」
チーム名は 「T4O」

チームの 長さを 表示する ※ 5
チームの 人数を 表示する ※ これもOK

チーム名の 文字数を 表示する ※ 3
チーム名の 匹数を 表示する   ※ 変だが、あり

チームの キー列を 表示する ※ {0: 0, 1: 1, 2: 2, 3: "サポート", 4: "リーダー"}

チームの 先頭を 表示する     ※ "アジューラ"
チームの 先頭以外を 表示する ※ {0: "チャールス", 1: "ウイ", "サポート": "ニッキー", "リーダー": "セフ"}
チームの 末尾を 表示する     ※ "セフ"
チームの 末尾以外を 表示する ※ {0: "アジューラ", 1: "チャールス", 2: "ウイ", "サポート": "ニッキー"}
```

### ブーリアン型

| ブーリアン値 | 使用可能キーワード         |
| ------------ | -------------------------- |
| True         | `真`、`肯定`、`はい`、`正` |
| False        | `偽`、`否定`、`いいえ`     |

### ヌル

使用可能キーワード：`無`、`無い`、`無し`、`ヌル`

### それ・あれ

[なでしこ](https://ja.wikipedia.org/wiki/なでしこ_%28プログラミング言語%29)と同じく、最後の実行した命令の結果を持つ`それ`というグローバルキーワードが使えます。

しかも、`あれ`というグローバルキーワードもあります。好きなように使ってください！

----

## 制限公文について

### 条件分岐

条件分岐は次のようなフォーマット：`もし 【条件式】`。分岐の中身は一個のホワイトスペース（全角・半角スペースまたはタブ）のインデントが必要です。詳しくは「[インデント](#インデント)」より参照してください。

条件式は普段、3つの部分に分けられます：主語、比較言1、比較言2。

主語は次のようなフォーマット：`【バリュー】が`。このバリューは一方の比較対象。

比較言1は他方の比較対象と接尾の`と`、`より`、`以上`、`以下`のどれか。

比較言2は`ならば`、`でなければ`、`等しければ`、`大きければ`、`小さければ`のどれか。`ならば`と`なら`は動作的に同様で、`でなければ`と`じゃなければ`も同様です。

比較演算子となる比較言1と比較言2の組み合わせは次のフォーマット：`【バリュー】【比較言1】 【比較言2】`。

下記は`Ａ`と`Ｂ`という変数のそれぞれの比較文：

| 比較文                             | 論理演算子        |
| ---------------------------------- | ----------------- |
| もし　Ａが　Ｂと　　等しければ     | `Ａ == Ｂ`        |
| もし　Ａが　Ｂより　大きければ     | `Ａ > Ｂ`         |
| もし　Ａが　Ｂより　小さければ     | `Ａ < Ｂ`         |
| もし　Ａが　Ｂ以上　ならば         | `Ａ >= Ｂ`        |
| もし　Ａが　Ｂ以下　ならば         | `Ａ <= Ｂ`        |
| もし　Ａが　Ｂと　　等しくなければ | `Ａ != Ｂ`        |
| もし　Ａが　Ｂ　　　ならば         | `Ａ == Ｂ`        |
| もし　Ａが　Ｂ　　　でなければ     | `Ａ != Ｂ`        |

`Ａ`と`Ｂ`のデータ型が違えば、論理演算子が`!=`の場合以外、比較文の結果が常に偽となります。

ちなみに、比較言2は漢字だけではなく、ひらがなでも書けます。

例）

```
もし Ａが Ｂと ひとしければ
　・・・
```

さらに、`大きければ`と`小さければ`は類語と入れ替えることが出来ます。

| より大きい (>) | より小さい (<) |
| -------------- | -------------- |
| 大きければ     | 小さければ     |
| 長ければ       | 短ければ       |
| 高ければ       | 低ければ       |
| 多ければ       | 少なければ     |

勿論、これらもひらがなで書けます。

#### ブーリアンコンテキストの条件

更に、一つの値にハテナマーク（全角・半角可能）を付けると、ブーリアン型にキャストされた値に対する条件分岐が可能です。該当する比較言2は`ならば`または`でなければ`です。

| 比較文                 | 論理演算子 |
| ---------------------- | ---------- |
| もし　Ａ？　ならば     | `Ａ`       |
| もし　Ａ？　でなければ | `!Ａ`      |

詳しくは「[ハテナマーク（疑問符）](#ハテナマーク疑問符)」より参照してください。

#### 空の条件

空かどうかの判断として、長さを`0`に比較出来ますが、そのコンテナーを`空`に比較することも可能です。ひらがなの`から`も使用可能です。

例）

```
もし 何かの配列の 長さが 0 ならば

※ 下記と同様

もし 何かの配列が 空 ならば
```

もし主語が配列または文字列でなければ、条件式の結果が常に偽となります。

#### 関数呼び出しの条件

3分の条件式と同じように、関数の呼び出しと接尾のハテナマーク（全角・半角可能）、そして比較言2の`ならば`は分岐条件として使えます。

例）

```
もし 「ふわふわ卵のヒレカツ丼」を 食べた？ ならば
　・・・
```

`でなければ`にすると、条件が反転される。

#### 「else if」と「else」

「もし」の条件分岐の後に、その文と同じインデントで並んだ「else if」または「else」も使えます。

「else if」は次のようなフォーマット：`もしくは 【条件式】`か`または 【条件式】`。条件式は上記の「もし」文と同じです。複数の「else if」文も可能です。

「else」は独立なキーワードですが、良い響きの為にエイリアスは色々あります：`それ以外ならば`、`それ以外なら`、`それ以外は`、`それ以外だと`、`でなければ`、`じゃなければ`、`違うならば`、`違うなら`、`違えば`。最後の3つだけはひらがな可能です。

例）

```
もし Ａが Ｂと 等しければ
　・・・
もしくは Ａが Ｂより 大きければ
　・・・
または Ａが Ｂより 小さければ
　・・・
それ以外は
　・・・
```

条件式を否定するには`でなければ`を使うのです。

### 複数条件分岐

(準備中)

### 繰り返し（反復）

反復のキーワードは`繰り返す`。どの漢字とひらがなの組み合わせでも可能です。

反復は二種類です。（任意の）引数のと、コンテナーのようなオブジェクトに対するのです。

`終わり`というキーワードで反復からすぐに離脱出来ます。`次`というキーワードで当回の繰り返しを終えて、次の繰り返しに飛べます。どちらもひらがなで書けます。

反復の中身は一個のホワイトスペース（全角・半角スペースまたはタブ）のインデントが必要です。詳しくは「[インデント](#インデント)」より参照してください。

#### 引数の有無での繰り返し

単純な反復は必ず、2つの引数（始点と終点）、または引数なしの2つのパターンとなる次のフォーマット：`【任意引数】 繰り返す`。

引数なしの際、`終わり`などで手動で離脱しないと無限ループになります。

2つの引数の際、両方は変数または数値のどれかを使えます。変数に対しては数値的であるべきというのですが、妥当性の確認がありません。浮動小数点は整数にキャストされます。更に、その引数は始点と終点と指定する「から」と「まで」という助詞を順番自由に使います。終点が含まれます。始点と終点を入れ替えることで逆順の反復が可能です。

例）

```
1から 100まで 繰り返す
　・・・

繰り返す
　「無限ループ？」を 言う
　終わり
```

#### オブジェクトに対する繰り返し

コンテナーのようなオブジェクトの中身に対して繰り返すのは次のようなフォーマット：`【オブジェクト】に 対して 繰り返す`。そのオブジェクトは配列系の変数または文字列であるべきなのですが、前者の際は妥当性の確認がありません。`対して`はひらがなでも書けます。

例）

```
買い物リストに 対して 繰り返す
　アイテムは それ
　もし アイテムを 既に買った？ ならば
　　次
　違えば
　　アイテムを 買う
```

### 例外処理

(準備中)

----

## 関数について

### 関数の定義

関数の定義は次のようなフォーマット：`【引数（任意）】 【関数名】とは`。

関数名は動詞（又は動詞句）で、同じスコープに重複の定義（ビルトイン関数も含む）は出来ません（※）。関数の中身は一個のホワイトスペース（全角・半角スペースまたはタブ）のインデントが必要です。詳しくは「[インデント](#インデント)」より参照してください。反復内には関数の定義が不可能です。

引数との助詞の指定も必要です。助詞は引数名に含まれません。使用可能助詞は`から`、`で`、`と`、`に`、`へ`、`まで`、`を`。

例）

```
友達と 食べ物を 道具で 食べるとは
　・・・
```

上記の`食べる`という関数には、`友達`、`食べ物`、と`道具`という引数があります。

一部の組み込み関数以外、関数の引数はコピーとして渡されます（詳しくは「[ビルトイン関数](#ビルトイン関数組み込み関数)」より参照してください）。外に定義した変数の値は読み込むことが可能ですが、代入が不可能です。引数または変数名が外のに被る際、関数のスコープが別のコピーを使用します。関数呼出終了後まで残したい値があれば、返す（下記のセクション）または特別グローバル`あれ`に代入することが推薦です。詳しくは「[それ / あれ](#それあれ)」より参照してください。

※関数の引数が使う助詞もシグナチャーに含まれます。助詞が違えば、同じ関数名を重複定義することが出来ます。ビルトイン関数や特別なキーワードは例外です。

#### 関数の復帰

関数の復帰は意味や使い方に異なる複数の方法があります。

値を返すことは次のようなフォーマット：`【返り値】を 返す`又は`【返り値】と なる`。「返す」という前者を引数無しで使うと`それ`が返されます。「なる」という後者の引数が必要です。

返り値を指定せずに返ることは次のようなフォーマット：`返る` or `戻る`。使い分けは意味のみです。

`返る`や`戻る`など、返り値を指定していない際、ヌルが自動的に返されます。

どのキーワードもひらがなでも書けます。

### 関数の呼び出し

関数を単純に関数名で呼び出します。デフォルト値の引数が提供されない為、関数のシグナチャーには引数があれば、その引数を渡さなければなりません。

```
友達と 話すとは
　・・・

「金魚草さん」と 話す
```

関数の呼び出しの引数の助詞が関数の定義と異なる順番の際には、引数が定義の順番通りに使われます。

例）

```
友達と 食べ物を 道具で 食べるとは
　・・・

「箸」で 「金魚草さん」と 「ふわふわ卵のヒレカツ丼」を 食べる
```

この為、意味不明的で奇妙な関数呼び出しが可能なので要注意です。

例）

```
一と 二に 三と 四を 混ぜるとは
　・・・

2に 4を 3と 1と 混ぜる
```

上記の呼び出しはかなり意味不明ながら、ちゃんと動作します。しかし、独自の助詞のある引数は期待通りに並んで渡されるのですが、2つもある「と」の助詞の引数は区別が付かないので呼び出した順番に渡されます。結果的に引数の順番は3、2、1、4となります。

「[変数について](#変数について)」に前述したとおり、呼び出した関数の戻り値が`それ`というグローバル変数に代入されます。

エラーが投げらた際には、ヌルが返されます（エラー投げの詳しくは「[約物（句読文字）](#約物句読文字)」より参照してください）。

### 活用（動詞の語形変化）

関数を定義するタイミングで、該当する「た形」と「て形」の活用も定義されます。五段動詞と一段動詞の区別が曖昧な為、「いる」と「える」で終わる動詞の際には、両方の活用形が使えます。

例）

```
食べ物を 食べるとは
　・・・

「ふわふわ卵のヒレカツ丼」を 食べた
「もうひとつのヒレカツ丼」を 食べて
「まだまだヒレカツ丼」を 食べって ※不正な活用だが可能
```

区別が付かない活用の関数が定義されてしまうことはあります。その際、構文解析のタイミングでエラーが投げられます。新しい関数の定義の最後にビックリマーク（全角・半角可能）を付けると、前に定義した関数の活用形を上書きします。上書きされても、前の関数の原形をそのまま呼び出すことが可能です。

```
商品を かうとは
　・・・

草を かるとは ※構文解析の時、エラー
　・・・

草を かるとは！ ※エラーなし　「かう」の活用形が上書きされる
　・・・

「芝生」を かう   ※「かう」の原形がまだ呼び出せる
「芝生」を かって ※「かる」に参照
```

こうしてビルトイン関数の活用形の上書きも可能ですが、非推薦です。

----

## 雑題

### インデント

インデントレベルはホワイトスペース文字数に対して数えられます。スクリプトのメインボディはインデントなしで、その下の関数定義・分岐・反復の中身は周りより一段階深くのが必要です。インデントのホワイトスペースは全角・半角スペースとタブの中では自由です。スパゲッティコードが好きな人なら選り取りも可能です。

入力機が有効なまま、スペースでインデント出来るので全角スペースは良いかもしれませんが、一バイトであるタブはそれぞれのエディターによって幅の表示が好きなように設定出来るので推薦です。

### 改行

ホワイトスペースが可能な所なら`\`を入力すると、改行を入れて次の行に続けることも可能です。`\`の後はホワイトスペースと改行のみ可能です。

### 無演算命令

Python の`pass`の同様に金魚草は`・・・`という無演算命令を提供しています。後回しの実装の為や空なブロックの意図を示す為に使われます。

### コメント

インラインコメントは`※`で始まります。

`(`と`)`に囲まれる文はブロックコメントとなります。全角カッコも使用可能です。

例）

```
予定は 「買い物」※本当はゲーセン

（仕様未定）
プロジェクトするとは
　・・・

（
　作者：金魚草さん
　日時：2018-01-01 09:00:00
　バージョン： 1.0.0
）
```

### 約物（句読文字）

#### ビックリマーク（感嘆符）

もういいや！とにかく動け！と思う時がありますね。関数の呼び出しの命令の最後にビックリマーク（全角・半角可能）を付けるとエラーが抑えられます（これに対する例外について、`投げる`という[組み込み関数](#ビルトイン関数組み込み関数)より参照してください）。

例）

```
食べ物を 食べるとは
　1を 0で 割る

「プラスチック」を 食べる　 ※エラー有り
「プラスチック」を 食べる！ ※エラー無し

「エラー」を 投げる！ ※ビックリマークあってもエラー有り
```

#### ハテナマーク（疑問符）

変数や関数の呼び出しの末尾にハテナマークを付けるとバリュー又は戻り値がブーリアン型にキャストされます（条件式での使い方に関しては「[条件分岐](#条件分岐)」より参照してください）。

例）

```
食べ物を 食べるとは
　・・・

「ふわふわ卵のヒレカツ丼」を 食べた？
ホゲは それ
```

下記も等しい：

```
「ふわふわ卵のヒレカツ丼」を 食べる
ホゲは それ？
```

配列の定義にもハテナマークが使えます。

```
ブーリアン型リストは 0？、1？、配列？、「」？、「あ」？、無？
※結果： 偽、真、偽、偽、真、偽
```

要注意：ここのハテナマークは等式計算のように見えますが、そうではなく、ブーリアン型キャストです。

```
ホゲは 1
ホゲは 2？ ※ホゲの値は「true」 - 1と2の比較ではなくて、2のブーリアン型キャスト
```

下記はブーライン型へのキャストの仕組み：

| 値             | ハテナマークの結果 |
| -------------- | ------------------ |
| 0              | 偽                 |
| 0以外          | 真                 |
| 空の配列       | 偽                 |
| 値を持つ配列   | 真                 |
| 空の文字列     | 偽                 |
| 字を持つ文字列 | 真                 |
| 無             | 偽                 |

ビックリマークが付いてない関数がエラーを投げた際、結果が偽となります。詳しくは上記のセクション「[ビックリマーク（感嘆符）](#ビックリマーク感嘆符)」より参照してください。

### スクリプト終了

トップレベルのスコープでしかスクリプト終了出来ません。キーワードと使い方は関数の復帰と同じです。詳しくは「[関数の復帰](#関数の復帰)」より参照してください。

値を返して終了する際、スクリプトの終了コードがデータ型に応じて決まります。

| データ型                     | 終了コード |
| ---------------------------- | ---------- |
| 数値                         | 整数の値   |
| 配列または文字列             | 要素の数   |
| ブーリアン型（真）           | 0          |
| ブーリアン型（偽）またはヌル | 1          |

### デバッグ

[語源](https://ja.wikipedia.org/wiki/バグ#語源)と同じように、`蛾`というコマンドが提供されています。このコマンドを使うと、その時点までのプログラム状態を全て出力します。ビックリマーク（全角・半角可能）を付けるとプログラムの実行が停止します。

一つの変数または値をそのまま出力するには、ビルトイン関数`データを ポイ捨てる`が使えます。ビックリマークを付けると、プログラムの実行が停止します。

ここのコマンドはデバグのコマンドラインオプションが有効の時のみ使用可能です。

----

## ビルトイン関数（組み込み関数）

### 出力

#### `言葉と 言う`, `言葉を 言う`

`言葉`を標準出力に送信する。`言葉を 言う`とは文法的な差のみ。

| 引数           | 戻り値 | ひらがな可能 |
| -------------- | ------ | ------------ |
| `言葉`: 文字列 | `言葉` | ○           |

#### `メッセージを 表示する`

`メッセージ`を標準出力に送信する。改行が付く。

| 引数                 | 戻り値       | ひらがな可能 |
| -------------------- | ------------ | ------------ |
| `メッセージ`: 型自由 | `メッセージ` | ☓           |

#### `データを ポイ捨てる`

デバグのコマンドラインオプションが有効の際：`データ`を標準出力に送信する。ビックリマーク（全角・半角可能）を付けると実行が停止する。

| 引数             | 戻り値   | ひらがな可能 |
| ---------------- | -------- | ------------ |
| `データ`: 型自由 | `データ` | ☓           |

### フォーマット

#### `フォーマット文に 引数を 書き込む`

`フォーマット文`の中、`〇`という代用文字を`引数`（配列または変数）に書き換える。文字通りの「〇」を書くには、前に`\`を付ける。

`引数`が配列の場合、同じ数の代用文字が必要。`引数`が単なる変数の場合、代用文字が一つ。

`〇`の後にカッコ付きのフォーマット文を入力すると、数値の引数のフォーマットも設定出来る。数値のフォーマット文は`A詰めB桁。C詰めD桁`です（小数点は全角・半角可能）。小数点の前は`B`桁で、足りない分が`A`で詰める。小数点の後は`D`桁で、足りない分が`C`で詰める。`A`、`C`、`D`を除くと`0`となる。`D`が`0`の場合、小数が省略される。小数点の前の数字の桁数が`B`より長い場合、省略されない。

`〇`の後に文字絽織のカッコを書くには、前に`\`を付ける。

例）`「〇（　詰め4桁.6桁）」に 49を 書き込む`だと、`　　　4.900000`となる。

| 引数                                       | 戻り値             | ひらがな可能             |
| ------------------------------------------ | ------------------ | ------------------------ |
| `フォーマット文`: 文字列<br>`引数`: 型自由 | 書き込まれた文字列 | `書きこむ` or `かきこむ` |

#### `数値を 精度に 切り上げる`, `数値を 精度に 切り下げる`, `数値を 精度に 切り捨てる`

この3つの関数はそれぞれ、処理が少し異なる。

* `切り上げる` - `数値`を自身以上の最も違い`N`桁に四捨五入する。
* `切り下げる` - `数値`を自身以下の最も近い`N`桁に四捨五入する。
* `切り捨てる` - `数値`を最も近い`N`桁に四捨五入する。（5以上は上がる、4以下は下がる。）

`精度`は次のようなフォーマットの文字列：

* `N桁`の場合：小数点の前の`N`桁に四捨五入する。
* `少数第N位`または`少数点第2位`の場合：諸数点の後の`N`桁に四捨五入する。

| 引数                           | 戻り値  | ひらがな可能                               |
| ------------------------------ | ------- | ------------------------------------------ |
| `数値`: 数値<br>`精度`: 文字列 | 数値    | `きりあげる`,`きりさげる`, or `きりすてる` |

#### `変数を 数値化する`

次のようなロジックによって、`変数`の対等の数値に変える：

| データ型 | 処理 |
| -------- | ---- |
| 数値     | そのままの数値（変更なし） |
| 文字列   | 数値としてパースした結果；パースできない場合、エラーが発生する |
| 配列     | 配列の長さ |
| その他   | ブーリアンコンテキストで、真→1，偽→0 |

| 引数           | 戻り値 | ひらがな可能 |
| -------------- | ------ | ------------ |
| `変数`: 型自由 | 数値   | ☓           |

#### `変数を 整数化する`

次のようなロジックによって、`変数`の対等の整数に変える：

| データ型 | 処理 |
| -------- | ---- |
| 数値     | 小数部を落とした値 |
| 文字列   | 整数としてパースした結果；パースできない場合、エラーが発生する |
| 配列     | 配列の長さ |
| その他   | ブーリアンコンテキストで、真→1，偽→0 |

| 引数           | 戻り値 | ひらがな可能 |
| -------------- | ------ | ------------ |
| `変数`: 型自由 | 整数   | ☓           |

### 文字列・配列の処理

#### `対象列に 要素を 押し込む`, `対象列に 要素を 追加する`

`要素`を`対象列`の末尾（最高指数）に押し込む。`対象列`が文字列の際：`要素`は文字列に限る。

`追加する`はエイリアスだ。

`対象列`が変更される。

| 引数                                         | 戻り値   | ひらがな可能   |
| -------------------------------------------- | -------- | -------------- |
| `対象列`: 配列または文字列<br>`要素`: 型自由 | `対象列` | `おしこむ`のみ |

#### `対象列から 引き出す`

`対象列`の最後（最高指数）の要素を引き出す。

`対象列`が変更される。

| 引数                       | 戻り値         | ひらがな可能             |
| -------------------------- | -------------- | ------------------------ |
| `対象列`: 配列または文字列 | 引き出した要素 | `引きだす` or `ひきだす` |

#### `対象列に 要素を 先頭から押し込む`

`要素`を`対象列`の先頭（最低指数）に押し込む。`対象列`が文字列の際：`要素`は文字列に限る。

`対象列`が変更される。

| 引数                                         | 戻り値   | ひらがな可能           |
| -------------------------------------------- | -------- | ---------------------- |
| `対象列`: 配列または文字列<br>`要素`: 型自由 | `対象列` | `先頭からおしこむ`のみ |

#### `対象列から 先頭を引き出す`

`対象列`の最初（最低指数）の要素を引き出す。

`対象列`が変更される。

| 引数                        | 戻り値         | ひらがな可能                         |
| --------------------------- | -------------- | ------------------------------------ |
| `対象列`: 配列または文字列  | 引き出した要素 | `先頭を引きだす` or `先頭をひきだす` |

#### `対象列から 要素を 抜く`, `対象列から 要素を 取る`

最初の`要素`を`対象列`から抜く。

`取る`はエイリアスだ。

`対象列`が変更される。

| 引数                                         | 戻り値     | ひらがな可能 |
| -------------------------------------------- | ---------- | ------------ |
| `対象列`: 配列または文字列<br>`要素`: 型自由 | 抜いた要素 | ○           |

#### `対象列から 要素を 全部抜く`, `対象列から 要素を 全部取る`

全ての`要素`を`対象列`から抜く。

`全部取る`はエイリアスだ。

`対象列`が変更される。

| 引数                                         | 戻り値           | ひらがな可能             |
| -------------------------------------------- | ---------------- | ------------------------ |
| `対象列`: 配列または文字列<br>`要素`: 型自由 | 抜いた要素の配列 | `全部ぬく` or `全部とる` |

#### `対象列に 要素列を 繋ぐ`, `対象列に 要素列を 結合する`

`要素列`を`対象列`の最後に繋ぐ。`要素列`と`対象列`は同型に限る。

`結合する`はエイリアスだ。結合した結果のキーについて詳しくは[連想配列](#連想配列ハッシュ)より参照してください。

| 引数                                                     | 戻り値                               | ひらがな可能 |
| -------------------------------------------------------- | ------------------------------------ | ------------ |
| `対象列`: 配列または文字列<br>`要素列`: 配列または文字列 | `対象列`の最後に`要素列`を繋いだ結果 | `つなぐ`のみ |

#### `要素列を ノリで 連結する`

`要素列`の要素をそれぞれ文字列化して、`ノリ`の区切りで繋ぐ。

| 引数                             | 戻り値   | ひらがな可能 |
| -------------------------------- | -------- | ------------ |
| `要素列`: 配列<br>`ノリ`: 文字列 | 文字列   | ☓           |

#### `対象列を 区切りで 分割する`

`対象列`を`区切り`で分けて、それぞれの部分を新しい配列で返す。

`対象列`が配列の場合：配列を持つ配列を返す。

`対象列`が文字列の場合：文字列の配列を返す。`区切り`が文字列。

| 引数                                         | 戻り値         | ひらがな可能 |
| -------------------------------------------- | -------------- | ------------ |
| `対象列`: 配列 or 文字列<br>`区切り`: 型自由 | 配列 or 文字列 | ☓           |

#### `対象列を 始点から 終点まで 切り抜く`

`対象列`を`始点`から`終点`まで切って、指定した部分を取り除く。終点が含まれる。

連想配列の場合、キーに関係なく挿入順で切る。

`始点`がマイナスの場合、`0`となる。`終点`が要素数より大きい場合、最後の指数となる。`始点`が`終点`以上の場合、空の配列または空の文字列を返す。

`対象列`が変更される。

| 引数                                                     | 戻り値               | ひらがな可能         |
| -------------------------------------------------------- | -------------------- | -------------------- |
| `対象列`: 配列 or 文字列<br>`始点`: 数値<br>`終点`: 数値 | 切り抜いた要素の配列 | 切りぬく or きりぬく |

#### `対象列で 要素を 探す`

`対象列`の最初の`要素`の該当するキーを返す。見つからない場合、`無`を返す。

| 引数                                       | 戻り値         | ひらがな可能 |
| ------------------------------------------ | -------------- | ------------ |
| `対象列`: 配列 or 文字列<br>`要素`: 型自由 | 文字列 or 数値 | 〇           |

#### `要素列を 並び順で 並び替える`

`並び順`で並び替えた`要素列`を返す。

`並び順`は`昇順`または`降順`という文字列。

並び替えた後でもそれぞれの要素の該当するキーが保持される。

配列の要素のタイプが異なる場合、文字列として比較される。文字列化について詳しくは[文字列補間](#文字列補間)より参照してください。

| 引数           | 戻り値             | ひらがな可能       |
| -------------- | ------------------ | ------------------ |
| `要素列`: 配列 | 並び替えた`要素列` | `ならびかえる`のみ |

### 算数

#### `被加数に 加数を 足す`, `加数を 足す`

加法。`被加数`を略すと`それ`に足す。

| 引数                           | 戻り値                 | ひらがな可能 |
| ------------------------------ | ---------------------- | ------------ |
| `被加数`: 数値<br>`加数`: 数値 | `加数`と`被加数`の加算 | ○           |

#### `被減数から 減数を 引く`, `減数を 引く`

減法。`被減数`を略すと`それ`から引く。

| 引数                           | 戻り値                 | ひらがな可能 |
| ------------------------------ | ---------------------- | ------------ |
| `被減数`: 数値<br>`減数`: 数値 | `減数`と`被減数`の減算 | ○           |

#### `被乗数に 乗数を 掛ける`, `乗数を 掛ける`

乗法。`被乗数`を略すと`それ`に掛ける。

| 引数                            | 戻り値                 | ひらがな可能 |
| ------------------------------- | ---------------------- | ------------ |
| `被乗数`: 数値<br>`乗数`: 数値  | `被乗数`と`乗数`の乗算 | ○           |

#### `被除数を 除数で 割る`, `除数で 割る`

除法。`被除数`を略すと`それ`を割る。

| 引数                            | 戻り値                 | ひらがな可能 |
| ------------------------------- | ---------------------- | ------------ |
| `被除数`: 数値<br>`除数`: 数値  | `被除数`と`除数`の除算 | ○           |

#### `被除数を 除数で 割った余りを求める`, `除数で 割った余りを求める`

剰余算。`被除数`を略すと`それ`を割った余りを求める。

| 引数                            | 戻り値                       | ひらがな可能 |
| ------------------------------- | ---------------------------- | ------------ |
| `被除数`: 数値<br>`除数`: 数値  | `被除数`を`除数`で割った余り | `わった余りを求める`,<br>`わったあまりを求める`,<br>or `わったあまりをもとめる` |

### 雑用

#### `エラーを 投げる`

`エラー`を標準エラー出力に送信し、例外を発生させる。

引数が不正な際、他の関数と同様にエラーが抑えられるが、ビックリマークを付けるとエラーが浮かび上がらない。その場合以外は、ビックリマークを付けても動作が変わらない。詳しくは「[ビックリマーク（感嘆符）](#ビックリマーク感嘆符)」より参照してください。

| 引数             | 戻り値 | ひらがな可能 |
| ---------------- | ------ | ------------ |
| `エラー`: 文字列 | 未定   | ○           |

#### `値を 乱数の種に与える`

乱数を発生させるアルゴリズムの種を指定する。

| 引数       | 戻り値 | ひらがな可能             |
| ---------- | ------ | ------------------------ |
| `種`: 数値 | 無     | `乱数の種にあたえる`のみ |

#### `最低値から 最大値まで の乱数を発生させる`

`最低値`と`最大値`の中から安全性の弱い乱数値を返す。`最大値`が含まれる。`最低値`と`最大値`が整数化される。

| 引数                             | 戻り値 | ひらがな可能 |
| -------------------------------- | ------ | ------------ |
| `最低値`: 数値<br>`最大値`: 数値 | 数値   | ☓           |