[English](./manual.md)

# 前置き

下手な日本語ですみませんです。

# 変数について

変数の定義は次のようなフォーマット：`【変数名】は 【バリュー】`。

例）

```
ホゲは 1
```

`ホゲ`という`1`のバリューを持つ変数が作られます。

## バリューとデータ型

変数は初期値と共に定義しなくてはなりません。単純なバリューを直接に引数として関数に渡すことが出来ます。

### 数値

数値は次のようなフォーマット：`/-?(\d+\.\d+|\d+)/`（即ちマイナスや浮動小数点数可能）

例）

```
ホゲは -3.14
```

### 文字列

文字列は`「`で始まって、`」`で終わります。

例）

```
ホゲは 「もじれつ」
```

`」`の前に`\`を付けると文字通りの`」`を書くことが出来ます。

例）

```
ホゲは 「文字列の中の「もじれつ\」」
```

複数行の文字列も可能です。先頭のスペース、末尾のスペース、あと改行も含まれて省略されます。`\n`または`￥ｎ`を書き込むと改行を挿入出来ます。

```
作文は 「こんにちは。
         今日の予定は特になし。
         週末にカツ丼を食べに行く。」

「カツ丼が好き。￥ｎ
　毎日食べても飽きない。」を 言う
```

#### 文字列の長さ

文字列の字数の取得は次のようなフォーマット：`【文字列｜変数】の 【長さ属性】`.

長さ属性のキーワードは：`長さ`、`大きさ`、`数`。どれもひらがなでも書けます。

```
男子達は 「チャールス」、「ウイ」
女子達は 「ニッキー」、「セフ」

男性の人数は 男子達の 数

男性の人数に 女子達の 数を 足す
```

#### 文字列補間

(v1.1.0 実装予定)

```
名前は 「世界」
「こんにちは【名前】！」を 言う
```

### 配列

配列は一つ以上のバリュー持つカンマ（全角・半角可能）区切りリストです。

例）

```
ホゲは 1、2、3
フガは 1,2,3
ピヨは 「あ」、「い」、「う」、1、2、3
```

`配列`というキーワードで空の配列の定義が出来ます。

例）

```
ホゲは 配列
```

リストが長すぎる際、カンマの後に折り返しが出来ます。

```
母音は 「あ」、
　　　 「い」、
　　　 「う」、
　　　 「え」、
　　　 「お」
```

複数行の文字列と同じく、先頭・末尾のスペースが無視されますが、ブロックコメントで揃わせることが出来ます。

```
参加者は ※
※「ウイ」、
　「チャールス」、
　「ジャック＆
　　アジューラ」
```

#### 配列の長さ

配列の長さは文字列の字数と同様です。詳しくは「文字列の長さ」より見てください。

#### 連想配列（ハッシュ）

(v1.1.0 実装予定)

### ブーリアン型

| ブーリアン値 | 使用可能キーワード         |
| ------------ | -------------------------- |
| True         | `真`、`肯定`、`はい`、`正` |
| False        | `偽`、`否定`、`いいえ`     |

### ヌル

使用可能キーワード：`無`、`無い`、`無し`、`ヌル`

### それ / あれ

[なでしこ](https://ja.wikipedia.org/wiki/なでしこ_%28プログラミング言語%29)と同じく、最後の実行した命令の結果を持つ`それ`というグローバルキーワードが使えます。

しかも、`あれ`というグローバルキーワードもあります。好きなように使ってください！

----

# 関数について

## 関数の定義

関数の定義は次のようなフォーマット：`【引数（任意）】 【関数名】とは`。

関数名は動詞（又は動詞句）で、同じスコープに重複の定義（ビルトイン関数も含まれ）は出来ません（※）。関数の中身は一個のホワイトスペース（全角・半角スペースまたはタブ）のインデントが必要です。詳しくは「インデント」より見てください。反復内には関数の定義が不可能です。

引数との助詞の指定も必要です。助詞は引数名に含まれません。使用可能助詞は`から`、`で`、`と`、`に`、`へ`、`まで`、`を`。

例）

```
友達と 食べ物を 道具で 食べるとは
　・・・
```

上記の`食べる`という関数には、`友達`、`食べ物`、と`道具`という引数があります。

※関数の引数が使う助詞もシグナチャーに含まれます。助詞が違えば、同じ関数名を重複定義することが出来ます。ビルトイン関数や特別なキーワードは例外です。

### 関数の復帰

関数の復帰は意味や使い方に異なる複数の方法があります。

値を返すことは次のようなフォーマット：`【返り値】を 返す`又は`【返り値】と なる`。「返す」という前者を引数無しで使うと`それ`が返されます。「なる」という後者の引数が必要です。

返り値を指定せずに返ることは次のようなフォーマット：`返る` or `戻る`。使い分けは意味のみです。

`返る`や`戻る`など、返り値を指定していない際、ヌルが自動的に返されます。

どのキーワードもひらがなでも書けます。

## 関数の呼び出し

関数を単純に関数名で呼び出します。デフォルト値の引数が提供されない為、関数のシグナチャーには引数があればその引数を渡さないとなりません。

```
友達と 話すとは
　・・・

「金魚草さん」と 話す
```

関数の呼び出しの引数の助詞が関数の定義と異なる順番の際には、引数が定義の順番通りに使われます。

例）

```
友達と 食べ物を 道具で 食べるとは
　・・・

「箸」で 「金魚草さん」と 「ふわふわ卵のヒレカツ丼」を 食べる
```

この為、意味不明的で奇妙な関数呼び出しが可能なので要注意です。

例）

```
一と 二に 三と 四を 混ぜるとは
　・・・

2に 4を 3と 1と 混ぜる
```

上記の呼び出しはかなり意味不明ながら、ちゃんと動作します。しかし、独自の助詞のある引数は期待通りに並んで渡されるのですが、2つもある「と」の助詞の引数は区別が付かないので呼び出した順番に渡されます。結果的に引数の順番は3、2、1、4となります。

「変数について」に前述したとおり、呼び出した関数の戻り値が`それ`というグローバル変数に代入されます。

エラーが投げらた際には、ヌルが返されます（エラー投げの詳しくは「約物（句読文字）」より見てください）。

## 活用（動詞の語形変化）

関数を定義するタイミングで、該当する「た形」と「て形」の活用も定義されます。五段動詞と一段動詞の区別が曖昧な為、「いる」と「える」で終わる動詞の際には、両方の活用形が使えます。

例）

```
食べ物を 食べるとは
　・・・

「ふわふわ卵のヒレカツ丼」を 食べた
「もうひとつのヒレカツ丼」を 食べて
「まだまだヒレカツ丼」を 食べって （変ながら可能
```

区別が付かない活用の関数が定義されてしまうことはあります。その際、構文解析のタイミングでエラーが投げられます。新しい関数の定義の最後にビックリマーク（全角・半角可能）を付けると、前に定義した関数の活用形を上書きします。上書きされても、前の関数の原形をそのまま呼び出すことが可能です。

```
商品を かうとは
　・・・

草を かるとは (構文解析の時、エラー
　・・・

草を かるとは！ (エラーなし　「かう」の活用形が上書きされる
　・・・

「芝生」を かう   (「かう」の原形がまだ呼び出せる
「芝生」を かって (「かる」に参照
```

こうしてビルトイン関数の活用形の上書きも可能ですが、非推薦です。

----

# 制限公文について

## 条件分岐

条件分岐は次のようなフォーマット：`もし 【条件式】`。分岐の中身は一個のホワイトスペース（全角・半角スペースまたはタブ）のインデントが必要です。詳しくは「インデント」より見てください。

条件式は普段、3つの部分に分けられます：比1、比2、比3（*比*較演算子）

比1は次のようなフォーマット：`【バリュー】が`。このバリューは一方の比較対象。

比2は他方の比較対象と接尾の`と`、`より`、`以上`、`以下`、とハテナマーク（全角・半角可能）のどれか。

比3は`ならば`、`でなければ`、`等しければ`、`大きければ`、`小さければ`のどれか。

比較演算子となる比2と比3の組み合わせは次のフォーマット：`【バリュー】【比2】 【比3】`。比2がハテナマークの際には比較演算子が`==`となります。該当する比3は`ならば`です。比3は`でなければ`の際、比較演算子が`!=`となります。

下記は`Ａ`と`Ｂ`という変数のそれぞれの比較文：

| 比較文                             | 論理演算子        |
| ---------------------------------- | ----------------- |
| もし　Ａが　Ｂと　　等しければ     | `Ａ == Ｂ`        |
| もし　Ａが　Ｂより　大きければ     | `Ａ > Ｂ`         |
| もし　Ａが　Ｂより　小さければ     | `Ａ < Ｂ`         |
| もし　Ａが　Ｂ以上　ならば         | `Ａ >= Ｂ`        |
| もし　Ａが　Ｂ以下　ならば         | `Ａ <= Ｂ`        |
| もし　Ａが　Ｂと　　等しくなければ | `Ａ != Ｂ`        |
| もし　Ａが　Ｂ？　　ならば         | `Ａ == Ｂ`        |
| もし　Ａが　Ｂ？　　でなければ     | `Ａ != Ｂ`        |
| もし　Ａ？　ならば                 | `Ａ`              |
| もし　Ａ？　でなければ             | `!Ａ`             |

ちなみに、比3は漢字だけではなく、ひらがなでも書けます。

例）

```
もし Ａが Ｂと ひとしければ
　・・・
```

さらに、`大きければ`と`小さければ`は類語と入れ替えることが出来ます。

| より大きい (>) | より小さい (<) |
| -------------- | -------------- |
| 大きければ     | 小さければ     |
| 長ければ       | 短ければ       |
| 高ければ       | 低ければ       |
| 多ければ       | 少なければ     |

勿論、これらもひらがなで書けます。

### 「else if」と「else」

「もし」の条件分岐の後に、その文と同じインデントで並んだ「else if」または「else」も使えます。

「else if」は次のようなフォーマット：`もしくは 【条件式】`か`または 【条件式】`。条件式は上記の「もし」文と同じです。複数の「else if」文も可能です。

「else」は独立なキーワードで、`それ以外`または`違えば`となります。後者だけはひらがなでも書けます。

例）

```
もし Ａが Ｂと 等しければ
　・・・
もしくは Ａが Ｂより 大きければ
　・・・
または Ａが Ｂより 小さければ
　・・・
それ以外
　・・・
```

### 関数呼び出しの条件

3分の条件式と同じように、関数の呼び出しと接尾のハテナマーク（全角・半角可能）、そして比3の`ならば`は分岐条件として使えます。

例）

```
もし 「ふわふわ卵のヒレカツ丼」を 食べた？ ならば
　・・・
```

条件式を否定するには`でなければ`を使うのです。

### 複数条件分岐

(v1.1.0 実装予定)

## 繰り返し（反復）

反復のキーワードは`繰り返す`。どの漢字とひらがなの組み合わせでも可能です。

反復は二種類です。（任意の）引数のと、コンテナーのようなオブジェクトに対するのです。

`終わり`というキーワードで反復からすぐに離脱出来ます。`次`というキーワードで当回の繰り返しを終えて、次の繰り返しに飛べます。どちらもひらがなで書けます。

反復の中身は一個のホワイトスペース（全角・半角スペースまたはタブ）のインデントが必要です。詳しくは「インデント」より見てください。

### 引数の有無での繰り返し

単純な反復は必ず、2つの引数（始点と終点）、または引数なしの2つのパターンとなる次のフォーマット：`【任意引数】 繰り返す`。

引数なしの際、`終わり`などで手動で離脱しないと無限ループになります。

2つの引数の際、両方は変数または数値のどれかを使えます。変数に対しては数値的であるべきというのですが、妥当性の確認がありません。更に、その引数は始点と終点と見なす「から」と「まで」という助詞を順番自由に使います。

例）

```
1から 100まで 繰り返す
　・・・

繰り返す
　「無限ループ？」を 言う
　終わり
```

### オブジェクトに対する繰り返し

コンテナーのようなオブジェクトの中身に対して繰り返すのは次のようなフォーマット：`【オブジェクト】に 対して 繰り返す`。そのオブジェクトは配列系の変数または文字列であるべきなのですが、前者の際は妥当性の確認がありません。`対して`はひらがなでも書けます。

例）

```
買い物リストに 対して 繰り返す
　アイテムは それ
　もし アイテムを 既に買った？ ならば
　　次
　違えば
　　アイテムを 買う
```

----

# 例外処理

(v1.1.0 実装予定)

----

# 雑題

## インデント

インデントレベルはホワイトスペース文字数に対して数えられます。スクリプトのメインボディはインデントなしで、その下の関数定義・分岐・反復の中身は周りより一段階深くのが必要です。インデントのホワイトスペースは全角・半角スペースとタブの中では自由です。スパゲッティコードが好きな人なら選り取りも可能です。

入力機が有効なまま、スペースでインデント出来るので全角スペースは良いかもしれませんが、一バイトであるタブはそれぞれのエディターによって幅の表示が好きなように設定出来るので推薦です。

## スクリプト終了

トップレベルのスコープでしかスクリプト終了出来ません。キーワードと使い方は関数の復帰と同じです。詳しくは「関数の復帰」より見てください。

## 無演算命令

Python の`pass`の同様に金魚草は`・・・`という無演算命令を提供しています。後回しの実装の為や空なブロックの意図を示す為に使われます。

## コメント

インラインコメントは`(`又は`（`で始まります。*（独り言のよう*

`※`に囲まれる文はブロックコメントとなります。

例）

```
予定は 「買い物」（本当はゲーセン

※仕様未定※
プロジェクトするとは
　・・・

※
　作者：金魚草さん
　日時：2018-01-01 09:00:00
　バージョン： 1.0.0
※
```

## 約物（句読文字）

### ビックリマーク（感嘆符）

関数の呼び出しにエラーが発生したらヌルが返されます。呼び出しの命令の最後にビックリマーク（全角・半角可能）を付けるとエラーが浮かび上がります（詳しくは「例外処理」より見てください）。

例）

```
食べ物を 食べるとは
　・・・

「プラスチック」を 食べる　（エラー無し
「プラスチック」を 食べる！（エラー有り
```

### ハテナマーク（疑問符）

変数や関数の呼び出しの末尾にハテナマークを付けるとバリュー又は戻り値がブーリアン型にキャストされます（条件式での使い方に関しては「条件分岐」より見て下さい）。

例）

```
食べ物を 食べるとは
　・・・

「ふわふわ卵のヒレカツ丼」を 食べた？
ホゲは それ
```

下記も等しい：

```
「ふわふわ卵のヒレカツ丼」を 食べる
ホゲは それ？
```

配列の定義にもハテナマークが使えます。

```
ブーリアン型リストは 0？、1？、配列？、「」？、「あ」？、無？
（結果： 真、偽、偽、偽、真、偽
```

要注意：「if」文での等式計算とは違って、ここのハテナマークはブーリアン型キャストです。

```
ホゲは 1
ホゲは 2？ (ホゲの値は「true」 - 1と2の比較ではなくて、2のブーリアン型キャスト
```

下記はブーライン型へのキャストの仕組み：

| 値             | ハテナマークの結果 |
| -------------- | ------------------ |
| 0              | 偽                 |
| 0以外          | 真                 |
| 空の配列       | 偽                 |
| 値を持つ配列   | 真                 |
| 空の文字列     | 偽                 |
| 字を持つ文字列 | 真                 |
| 無             | 偽                 |

## デバッグ

[語源](https://ja.wikipedia.org/wiki/バグ#語源)と同じように、`蛾`というコマンドが提供されています。このコマンドを使うと、その時点までのプログラム状態を全て出力します。ビックリマーク（全角・半角可能）を付けるとプログラムの実行が停止します。

一つの変数または値をそのまま出力するには、ビルトイン関数`データを ポイ捨てる`が使えます。ビックリマークを付けると、プログラムの実行が停止します。

ここのコマンドはデバグのコマンドラインオプションが有効の時のみ使用可能です。

----

# ビルトイン関数（組み込み関数）

| 関数シグネチャ                       | 仕様                                         | ひらがな可能      |
| ------------------------------------ | -------------------------------------------- | ----------------- |
| `言葉と 言う`                        | 標準出力（文字列のみ）                       | ○               |
| `言葉を 言う`                        | 〃（文法的な差のみ)                          | ○               |
| `メッセージを 表示する`              | 標準出力（変数型は無制限）                   | ☓                |
| `データを ポイ捨てる`                | デバッグが有効の場合：標準出力               | ☓                |
| `メッセージを ログする`              | ログに出力 / `console.log` / 等              | ☓                |
| `エラーを 投げる`                    | 標準エラー出力、例外を発生させる             | ○               |
| `要素を 対象列に 追加する`           | 配列・文字列対応                             | ☓                |
| `要素列を 対象列に 連結する`         | 配列・文字列対応                             | ☓                |
| `対象列から 要素を 抜く`             | 最初の`要素`を`対象列`から抜く               | ○               |
| `対象列から 要素を 全部抜く`         | 全ての`要素`を`対象列`から抜く               | `全部ぬく`のみ   |
| `対象列に 要素を 押し込む`           | `要素`を`対象列`の末尾（最高指数）に押し込む | `おしこむ`のみ   |
| `対象列から 抜き出す`                | `対象列`の最後（最高指数）の要素を抜き出す   | `抜きだす` or `ぬきだす` |
| `対象列に 要素を 先頭から押し込む`   | `要素`を`対象列`の先頭（指数：0）に押し込む  | `先頭からおしこむ`のみ |
| `対象列から 先頭を抜き出す`          | `対象列`の最初（指数：0）の要素を抜き出す    | `先頭を抜きだす` or `先頭をぬきだす` |
| `被加数に 加数を 足す`               | 加法                                         | ○               |
| `加数を 足す`                        | 〃（被加数は`それ`）                         | ○               |
| `被減数から 減数を 引く`             | 減法                                         | ○               |
| `減数を 引く`                        | 〃（被減数は`それ`）                         | ○               |
| `被乗数に 乗数を 掛ける`             | 乗法                                         | ○               |
| `乗数を 掛ける`                      | 〃（被乗数は`それ`）                         | ○               |
| `被除数を 除数で 割る`               | 除法                                         | ○               |
| `除数で 割る`                        | 〃（被除数は`それ`）                         | ○               |
| `被除数を 除数で 割った余りを求める` | 剰余算                                       | `わった余りを求める`, `わったあまりを求める` or `わったあまりをもとめる` |
| `除数で 割った余りを求める`          | 〃（被除数は`それ`）                         | `わった余りを求める`, `わったあまりを求める` or `わったあまりをもとめる` |